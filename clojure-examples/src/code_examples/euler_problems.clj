(ns code-examples.euler-problems)

; Multiples of 3 and 5
;
; If we list all the natural numbers below 10 that are multiples of 3 or 5, we
; get 3, 5, 6 and 9. The sum of these multiples is 23.
;
; Find the sum of all the multiples of 3 or 5 below 1000.
;
; ANSWER: 233168

(reduce + (set (concat (range 0 1000 3) (range 0 1000 5))))

; Even Fibonacci numbers
;
; Each new term in the Fibonacci sequence is generated by adding the previous
; two terms. By starting with 1 and 2, the first 10 terms will be:
;
; 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
;
; By considering the terms in the Fibonacci sequence whose values do not exceed
; four million, find the sum of the even-valued terms.
;
; ANSWER: 4613732

(defn fib [[a b]] [b (+ a b)])

(reduce +
  (take-while (partial > 4000000)
    (filter even?
      (map first (iterate fib [1 2])))))

; Largest palindrome product
;
; A palindromic number reads the same both ways. The largest palindrome made
; from the product of two 2-digit numbers is 9009 = 91 99.
;
; Find the largest palindrome made from the product of two 3-digit numbers.
;
; ANSWER: 906609

(defn palindrome? [chars]
  (if (< (count chars) 2) true
    (and
      (= (first chars) (last chars))
      (recur (-> chars rest butlast))))) 

(let [three-digit-numbers (range 100 1000)]
  (reduce max
    (filter (comp palindrome? str)
      (set
        (for [a three-digit-numbers
              b three-digit-numbers]
          (* a b))))))

; Smallest multiple
;
; 2520 is the smallest number that can be divided by each of the numbers from 1
; to 10 without any remainder.
;
; What is the smallest positive number that is evenly divisible by all of the
; numbers from 1 to 20?
;
; ANSWER: 232792560

(defn divides-evenly? [number divisor]
  (= 0 (rem number divisor)))

(defn all-divide-evenly? [divisors number]
  (every? (partial divides-evenly? number) divisors))

(first
  (filter (partial all-divide-evenly? (range 1 21))
    (iterate (partial + 20) 20)))

